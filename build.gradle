import org.apache.tools.ant.filters.ReplaceTokens
import java.nio.charset.StandardCharsets

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'application'

group 'fr.fistin'
version '1.2.0-SNAPSHOT'
archivesBaseName = 'hydra'
mainClassName = 'fr.fistin.hydra.HydraBootstrap'

compileJava {
    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = StandardCharsets.UTF_8
}

repositories {
    mavenCentral()

    maven {
        name = 'HydraConnector_maven'
        url 'https://maven.pkg.github.com/FistinNetwork/HydraConnector/'
        credentials {
            username System.getenv('USERNAME')
            password System.getenv('TOKEN')
        }
    }
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

dependencies {
    implementation 'com.github.docker-java:docker-java:3.2.5'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.2.5'
    implementation 'org.yaml:snakeyaml:1.28'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    implementation 'jline:jline:2.12'
    implementation 'fr.fistin:hydra-connector:1.1.3-SNAPSHOT'
}


